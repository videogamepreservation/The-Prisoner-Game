 1 ZZ =  FRE (0): POKE  - 16368,0
 2  ONERR  GOTO 32000
 5  PRINT D$;"BLOAD TONES"
 9  GOTO 1000
 395 T1% = L4% - L3% + 1: VTAB H3%: FOR I3 = H3% TO H4%: HTAB L3%: PRINT  SPC( T1%): PRINT : NEXT I3: RETURN 
 600  VTAB 14: HTAB 3: PRINT  SPC( 36): HTAB 3: FOR I6 = 1 TO  LEN (TP$):A$ =  MID$ (TP$,I6,1):A% =  ASC (A$) - 31: PRINT A$;: POKE P1,A%: POKE P2,A%: CALL P3: NEXT I6: POKE  - 16368,0: FOR I6 = 1 TO 1000: NEXT I6: RETURN 
 610  VTAB 14: HTAB 3: PRINT  SPC( 36): HTAB 3:CT% = 0:IP$ = ""
 613  GET A$: IF A$ <  >  CHR$ (13) AND A$ <  >  CHR$ (8) THEN  PRINT A$;:IP$ = IP$ + A$:CT% = CT% + 1
 615  IF A$ <  >  CHR$ (13) AND CT% < 36 THEN 613
 616  IF  NOT ( LEN (IP$)) THEN IP$ = " "
 618  RETURN 
 620  INVERSE :L3% = 8:L4% = 23:H3% = 6:H4% = 11: GOSUB 395: VTAB 6: HTAB 10: PRINT "THE ISLANDER"
 623 CT% = 17: IF  LEN (TP$) < 17 THEN  HTAB 8: PRINT TP$: NORMAL : RETURN 
 625  IF  MID$ (TP$,CT%,1) <  > " " THEN CT% = CT% - 1: GOTO 625
 628 A$ =  MID$ (TP$,1,CT% - 1): HTAB 8: PRINT A$:TP$ =  RIGHT$ (TP$, LEN (TP$) - CT%): GOTO 623
 630  VTAB 8: HTAB 24: PRINT  SPC( 15): HTAB 24: PRINT TP$: RETURN 
 640  INVERSE : VTAB 18: HTAB 26: PRINT  CHR$ (7);T1$;: FOR I6 = 1 TO 1000: NEXT I6: HTAB 26: PRINT  SPC( 10): NORMAL : RETURN 
 1000  GOSUB 10000
 1003  GOSUB 2000
 1005  NORMAL : PRINT : PRINT CH$
 1008  CALL 520"PR.VILLAGE"
 1999  END 
 2000 TP$ = "GOOD DAY, #": GOSUB 600
 2001 TP$ = "WHICH PAPER WOULD YOU LIKE?": GOSUB 600: GOSUB 610: IF IP$ <  > "THE ISLANDER" THEN TP$ = "SORRY, WE ONLY CARRY THE ISLANDER": GOSUB 600:TP$ = "WOULD YOU LIKE A COPY?": GOSUB 600: GOSUB 610: IF  LEFT$ (IP$,1) = "N" THEN 2090
 2003  IF  NOT (CR%) THEN TP$ = "IT SEEMS YOU'RE SHORT ON CHANGE": GOSUB 600: GOTO 2090
 2005 CR% = CR% - 1:TP$ = "THAT'LL BE ONE CREDIT": GOSUB 600
 2008  IF MR% AND  INT ( RND (1) * 2) THEN TP$ = "# LINKED WITH MURDER": GOSUB 620: GOTO 2013
 2010  ON  INT ( RND (1) * 11) + 1 GOSUB 13001,13002,13003,13004,13005,13006,13007,13008,13009,13010,13011: GOSUB 620
 2013 TP$ = "IS THERE ANYTHING ELSE YOU NEED?": GOSUB 600: GOSUB 610: IF  LEFT$ (IP$,1) = "Y" THEN 2001
 2015  IF BR% <  > 2 OR  LEN (IP$) < 5 THEN 2090
 2018 CT% = 0: FOR I = 1 TO  LEN (IP$) - 4:A$ =  MID$ (IP$,I,5): IF A$ = "PRINT" OR A$ = "WORK " THEN CT% = 1
 2020  NEXT I: IF  NOT (CT%) THEN TP$ = "I ONLY SELL PAPERS OR DO PRINTING": GOSUB 600: GOTO 2090
 2023  IF  NOT (PS%(12)) THEN TP$ = "YOU'LL HAVE TO GET SOME PAPER FIRST": GOSUB 600: GOTO 2090
 2025 TP$ = "COME AROUND BACK": GOSUB 600: GOTO 3000
 2090 TP$ = "BE SEEING YOU": GOSUB 600: RETURN 
 3000  TEXT : HOME : INVERSE : PRINT  SPC( 200): VTAB 2: HTAB 2: PRINT "NO GOVERNMENT OUGHT TO BE WITHOUT": HTAB 2: PRINT "CENSORS; AND WHERE THE PRESS IS FREE"
 3003  HTAB 2: PRINT "NO ONE EVER WILL - THOMAS JEFFERSON":L3% = 4:L4% = 20:H3% = 7:H4% = 14: GOSUB 395:H4% = 9:L3% = 23:L4% = 39: GOSUB 395: FOR I = 1 TO 2: HTAB 31: PRINT " ": NEXT I:H3% = 12:H4% = 19:L3% = 25:L4% = 36: GOSUB 395
 3004  VTAB 13: HTAB 26: PRINT "NUMBER:": VTAB 8: HTAB 6: PRINT "THE ISLANDER"
 3005  VTAB 14: HTAB 26: PRINT "LETTER:": HTAB 26: PRINT "X TO ERASE": HTAB 26: PRINT "E TO ADD": HTAB 26: PRINT "L TO LEAVE": NORMAL : VTAB 8: HTAB 24: PRINT  SPC( 15): VTAB 13: HTAB 33: PRINT "  "
 3007  VTAB 21: HTAB 1: PRINT "SET UP YOUR TYPE ON THE PRESS"
 3008 TP$ = "":IP$ = ""
 3010 A% =  PEEK ( - 16384): IF A% < 127 THEN 3010
 3012  POKE  - 16368,0:A% = A% - 128: IF A% = 76 THEN  RETURN 
 3013  IF A% = 88 AND  LEN (TP$) > 1 THEN TP$ =  LEFT$ (TP$, LEN (TP$) - 1): GOSUB 630: GOTO 3010
 3014  IF A% = 88 AND  LEN (TP$) THEN TP$ = "": GOSUB 630: GOTO 3010
 3015  IF A% <  > 69 THEN 3020
 3017  IF  VAL (IP$) < 32 OR  VAL (IP$) > 90 THEN T1$ = "BAD VALUE": GOSUB 640: GOTO 3010
 3018 TP$ = TP$ +  CHR$ ( VAL (IP$)):IP$ = "": GOSUB 630: IF  LEN (TP$) < 15 THEN 3010
 3019  GOTO 3050
 3020  IF A% < 48 OR A% > 57 THEN T1$ = "BAD ENTRY": GOSUB 640: GOTO 3010
 3023  IF  LEN (IP$) = 2 THEN IP$ = ""
 3025 IP$ = IP$ +  CHR$ (A%): VTAB 13: HTAB 33: PRINT  SPC( 2): HTAB 33: PRINT IP$: IF  LEN (IP$) = 2 THEN  VTAB 14: HTAB 33: IF  VAL (IP$) > 31 AND  VAL (IP$) < 91 THEN  INVERSE : PRINT  CHR$ ( VAL (IP$)): NORMAL : GOTO 3027
 3026  IF  LEN (IP$) = 2 THEN T1$ = "BAD VALUE": GOSUB 640
 3027  IF  NOT ( INT ( RND (1) * 200)) AND  RND (1) * 500 > ID% THEN 3060
 3028  GOTO 3010
 3050  IF TP$ <  > "DEUS EX MACHINA" THEN  RETURN 
 3053 BR% = 5: VTAB 10: HTAB 4: PRINT TP$
 3060  POKE P1,255: POKE P2,255: FOR I = 1 TO 10: CALL P3:A% =  PEEK ( - 16384): IF A% > 127 THEN  POKE  - 16368,0
 3063 A% = A% - 128: IF A% = 76 THEN I = 20: NEXT I: RETURN 
 3065  NEXT I: NORMAL : PRINT : PRINT CH$
 3068  CALL 520"PR.SQ1"
 10000  TEXT : HOME : INVERSE : FOR I = 0 TO 3: HTAB 8 - I * 2: PRINT  SPC( 26 + I * 4): PRINT : NEXT I: FOR I = 1 TO 8: HTAB 5: PRINT " ";: HTAB 36: PRINT " ": NEXT I
 10003  FOR I = 1 TO 3: HTAB 2: PRINT  SPC( 38): PRINT : NEXT I:H3% = 16:H4% = 23:L3% = 5:L4% = 36: GOSUB 395:L3% = 8:L4% = 23:H3% = 6:H4% = 11: GOSUB 395
 10005  NORMAL : VTAB 14: HTAB 3: PRINT  SPC( 36):L3% = 15:L4% = 26:H3% = 17:H4% = 19: GOSUB 395: VTAB 18: HTAB 16: PRINT "NEWS STAND": RETURN 
 13001 TP$ = "PROF. GAUNTLET AWARDS DIPLOMA": RETURN 
 13002 TP$ = "BIG WINNER AT SLOT MACHINES": RETURN 
 13003 TP$ = "BANK INCREASES SAVINGS INTERESTS": RETURN 
 13004 TP$ = "TOWN HALL RETIREE GETS GOLD WATCH": RETURN 
 13005 TP$ = "LIBRARY NEEDS CONTRIBUTION OF BOOKS": RETURN 
 13006 TP$ = "CARETAKER URGES UNITY": RETURN 
 13007 TP$ = "REC HALL BOASTS FINE FITNESS PROGRAM": RETURN 
 13008 TP$ = "THEATER PROMISES ESCAPIST ENTERTAINMENT": RETURN 
 13009 TP$ = "THE COMMITTEE TO MEET TODAY": RETURN 
 13010 TP$ = "CARETAKER UNFOLDS HIS 'PROGRAMMA INTERNATIONAL'": RETURN 
 13011 TP$ = "CARETAKER FORSEES A RAINBOW OF GENE MANIPULATION TECHNIQUES": RETURN 
 32000  CALL 1002: IF  PEEK (222) <  > 255 THEN  RESUME 
 32003  NORMAL : PRINT : PRINT CH$
 32005  CALL 520"PR.SAVE"
