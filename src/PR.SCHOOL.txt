 1  TEXT : HOME :ZZ =  FRE (0): POKE  - 16368,0
 2  ONERR  GOTO 32000
 4  PRINT D$;"BLOAD TONES"
 9  GOTO 1000
 400  FOR I4 = 0 TO 1: FOR J = 0 TO 1: HLIN 6,9 AT I4 * 8 + 8 + J: NEXT J: VLIN 8,16 AT I4 * 3 + 6: NEXT I4: RETURN 
 410  FOR J = 0 TO 1: VLIN 8,16 AT 14:: HLIN 13,15 AT 16 + J: PLOT 13,8 + J: NEXT J: RETURN 
 420  FOR J = 0 TO 1: FOR I4 = 0 TO 1: HLIN 19,21 AT I4 * 8 + 8 + J: NEXT I4: FOR I4 = 0 TO 2: PLOT 21 - I4,10 + I4 * 2 + J: NEXT I4,J: RETURN 
 430  FOR J = 0 TO 1: FOR I4 = 0 TO 2: HLIN 25,27 AT I4 * 4 + 8 + J: NEXT I4: VLIN 8,16 AT 27: PLOT 26,12 + J: NEXT J: RETURN 
 440  VLIN 8,12 AT 30: VLIN 8,17 AT 32: FOR J = 0 TO 1: HLIN 30,33 AT 12 + J: NEXT J: RETURN 
 450  FOR J = 0 TO 1: FOR I4 = 0 TO 2: HLIN 6,9 AT I4 * 4 + 14 + J: NEXT I4: PLOT 6,16 + J: PLOT 9,20 + J: NEXT J: RETURN 
 460  VLIN 14,23 AT 12: VLIN 18,23 AT 15: FOR I4 = 0 TO 1: FOR J = 0 TO 1: HLIN 12,15 AT 18 + I4 * 4 + J: NEXT J,I4: RETURN 
 470  FOR I4 = 0 TO 1: HLIN 18,21 AT 14 + I4: FOR J = 0 TO 3: PLOT 21 - J,16 + J * 2 + I4: NEXT J,I4: RETURN 
 480  FOR I4 = 0 TO 1: FOR J = 0 TO 2: HLIN 24,27 AT J * 4 + 14 + I4: NEXT J: VLIN 14,23 AT I4 * 3 + 24: NEXT I4: RETURN 
 490  VLIN 14,19 AT 30: VLIN 14,23 AT 33: FOR I4 = 0 TO 1: FOR J = 0 TO 1: HLIN 30,33 AT 14 + I4 * 4 + J: NEXT J,I4: RETURN 
 531  FOR I5 = 1 TO 500: NEXT I5: RETURN 
 532  FOR I5 = 1 TO 50: NEXT I5: RETURN 
 600  NORMAL : FOR I = 1 TO  LEN (TP$): VTAB H1%: HTAB L1% + I - 1: PRINT  MID$ (TP$,I,1): GOSUB 610: FOR J = 1 TO 40: NEXT J,I:A% =  PEEK ( - 16384): IF A% = 155 THEN  POP 
 601  RETURN 
 610  VTAB  INT ( RND (1) * 3) + 21: HTAB  INT ( RND (1) * 5) + 3: PRINT  INT ( RND (1) * 10): RETURN 
 620  ON A% + 1 GOSUB 400,410,420,430,440,450,460,470,480,490: RETURN 
 630  POKE P1,(A% + 1) * 10: POKE P2,(A% + 1) * 10: CALL P3: RETURN 
 640  POKE P1,100: POKE P2,17: CALL P3: RETURN 
 1000  GOSUB 10000
 1005  GOSUB 11000
 1010  GOSUB 2000
 1080  PRINT CH$
 1085  CALL 520"PR.VILLAGE"
 1999  END 
 2000 L1% = 22:TP$ = "MEMORIZATION": GOSUB 600
 2003 L1% = 4:H1% = 7:TP$ = "YOU WILL BE GIVEN A SEQUENCE OF": GOSUB 600:H1% = 8:TP$ = "NUMBERS TO MEMORIZE AND REPEAT BACK"
 2005  GOSUB 600:H1% = 9:TP$ = "WHEN THE SEQUENCE IS FINISHED": GOSUB 600:H1% = 10:TP$ = "AND IT IS 'YOUR TRY'": GOSUB 600
 2007 H1% = 11:TP$ = "YOU MAY BEGIN AS SOON AS YOU HEAR": GOSUB 600:H1% = 12:TP$ = "THE BELL":CT% = 0: GOSUB 600
 2008 A% =  PEEK ( - 16384):CT% = CT% + 1: IF CT% = 300 THEN  PRINT "";
 2009  IF A% < 127 THEN  GOSUB 610: GOTO 2008
 2010  POKE  - 16368,0: IF CT% < 300 THEN ID% = ID% + 20
 2011  IF CT% > 400 THEN ID% = ID% - 5
 2015  GR : COLOR= 15: FOR I = 0 TO 39: HLIN 0,39 AT I: NEXT I: COLOR= 0: FOR I = 6 TO 24: HLIN 2,37 AT I: NEXT I:CT% = 0:T2% = 0
 2020  GOSUB 531: IF  NOT ( INT ( RND (1) * 25)) THEN TP$ = RC$: GOTO 2030
 2023 TP$ = "": FOR I = 1 TO T2%:TP$ = TP$ +  STR$ ( INT ( RND (1) * 10)): NEXT I: IF TP$ = RC$ THEN 2023
 2029  INVERSE : VTAB 23: HTAB 30: PRINT  SPC( 8): NORMAL 
 2030  FOR I = 1 TO  LEN (TP$):A$ =  MID$ (TP$,I,1):A% =  VAL (A$): COLOR= A% + 2: GOSUB 620: GOSUB 610: GOSUB 630: GOSUB 531: GOSUB 610: COLOR= 0: GOSUB 620: NEXT I:T1$ = "":T4% =  LEN (TP$)
 2031  INVERSE : VTAB 23: HTAB 30: PRINT  CHR$ (7);"YOUR TRY": NORMAL : POKE  - 16368,0
 2033 A% =  PEEK ( - 16384): IF  INT ( RND (1) * 1.2) THEN  GOSUB 610
 2035  IF A% < 127 THEN 2033
 2038  POKE  - 16368,0:A% = A% - 176: IF A% < 0 OR A% > 9 THEN 2045
 2040 T1$ = T1$ +  STR$ (A%): COLOR= A% + 2: GOSUB 620: GOSUB 610: GOSUB 630: GOSUB 531: GOSUB 610: COLOR= 0: GOSUB 620:T4% = T4% - 1: IF T4% THEN 2033
 2043  IF T1$ = RC$ THEN  POP : GOTO 62000
 2045  IF T1$ <  > TP$ THEN  FOR I = 1 TO 20: GOSUB 640: NEXT I:CT% = CT% + 1: IF CT% < 3 THEN 2020
 2048  IF CT% < 3 THEN CT% = 0:T2% = T2% + 1: GOTO 2020
 2050  GOSUB 10000:H1% = 5:TP$ = "IGNORANCE IS STRENGTH": GOSUB 600
 2055  IF T2% > 8 THEN H1% = 7:TP$ = "FOR YOUR PERSEVERENCE": GOSUB 600:H1% = 8:TP$ = "YOU WILL RECIEVE": GOSUB 600:TP$ = "YOUR DIPLOMA":H1% = 9: GOSUB 600:PS%(3) = 1
 2060  RETURN 
 2999  RETURN 
 10000  TEXT : VTAB 1: INVERSE : FOR I = 1 TO 23: PRINT  SPC( 40): NEXT I: NORMAL : VTAB 4: FOR I = 1 TO 10: HTAB 3: PRINT  SPC( 36): PRINT : NEXT I
 10003  FOR I = 21 TO 23: FOR J = 3 TO 7: VTAB I: HTAB J: PRINT  INT ( RND (1) * 10): NEXT J,I
 10005  FLASH : FOR I = 1 TO 4: VTAB 21: HTAB I * 2 + 30: PRINT " ";: NEXT I
 10008  INVERSE : VTAB 22: HTAB 9: PRINT "PROF. GAUNTLET"
 10050  FOR I = 1 TO 20: POKE P1,45: POKE P2,35: CALL P3: NEXT I
 10099  RETURN 
 11000 H1% = 5:L1% = 4:TP$ = "CLASS HAS BEGUN": GOSUB 600:H1% = 6:TP$ = "TODAY'S LESSON IS": GOSUB 600: RETURN 
 32000 T1% =  PEEK (222): CALL 1002: IF T1% = 5 OR T1% = 6 OR T1% = 7 OR T1% = 13 THEN  RESUME 
 32003  NORMAL : PRINT : PRINT CH$
 32005  IF T1% = 255 THEN  CALL 520"PR.SAVE"
 32008  CALL 520"PR.ERR"
 62000  PRINT CH$
 62003  CALL 520"PR.LOSE"
