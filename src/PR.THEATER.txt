 1 ZZ =  FRE (0): POKE  - 16368,0
 2  ONERR  GOTO 32000
 3  PRINT  CHR$ (4);"BLOAD TONES"
 5  ROT= 0: SCALE= 2: POKE 232,0: POKE 233,86
 9  GOTO 1000
 500  COLOR=  INT ( RND (1) * 16):T1% =  INT ( RND (1) * 14) + 8:T2% =  INT ( RND (1) * 20) + 9: POKE  - 16297,0: PLOT T2%,T1%: CALL P3: POKE  - 16298,0: RETURN 
 600  VTAB 22: HTAB 2: NORMAL : PRINT  SPC( 38):TP$ = BK$ + TP$ + BK$:T1% =  LEN (TP$) - 37:ZZ =  FRE (0): FOR I6 = 1 TO T1%
 603  HTAB 2: PRINT  MID$ (TP$,I6,38);: POKE P1,34 +  INT ( RND (1) * 26): POKE P2,25: GOSUB 500: NEXT I6:ZZ =  FRE (0): RETURN 
 1000 CT% = 0: GOSUB 10000
 1003  GOSUB 2000
 1005  TEXT : HOME : NORMAL : PRINT : PRINT CH$
 1008  CALL 520"PR.VILLAGE"
 1999  END 
 2000 A% =  PEEK ( - 16384):T1% =  INT ( RND (1) * 14) + 8:T2% =  INT ( RND (1) * 20) + 9: POKE P1,T1%: POKE P2,T2%: GOSUB 500
 2001 CT% = CT% + 1:T1% =  INT (CT% / 100) * (CT% / 100 =  INT (CT% / 100)): IF T1% THEN  ON T1% GOSUB 12100,12200,12300
 2004  IF A% < 128 THEN 2000
 2005  POKE  - 16368,0: IF A% = 155 THEN  RETURN 
 2010  POP : TEXT : HOME : NORMAL : PRINT : PRINT CH$
 2013  CALL 520"PR.BROTH"
 10000  TEXT : HOME : GR : HGR : POKE  - 16301,0: POKE  - 16301,0: POKE  - 16297,0: COLOR= 15: FOR I = 6 TO 7: HLIN 8,29 AT I: HLIN 8,28 AT I + 16: NEXT I: VLIN 6,23 AT 8: VLIN 6,23 AT 29: VLIN 22,39 AT 19
 10003  INVERSE : VTAB 21: PRINT  SPC( 120): VTAB 21: HTAB 15: PRINT "THE THEATER": VTAB 23: HTAB 10: PRINT "PRESS ESC KEY TO EXIT": VTAB 22: NORMAL : HTAB 2: PRINT  SPC( 38)
 10005  HCOLOR= 7: FOR I = 24 TO 31: HPLOT 56,I TO 209,I: NEXT I: FOR I = 88 TO 95: HPLOT 56,I TO 209,I: NEXT I: FOR I = 56 TO 61: HPLOT I,24 TO I,95: NEXT I: FOR I = 203 TO 209: HPLOT I,24 TO I,95: NEXT I
 10008  FOR I = 133 TO 139: HPLOT I,88 TO I,159: NEXT I: DRAW 2 AT 123,48: SCALE= 1: RETURN 
 12100  DRAW 1 AT 168,56:TP$ = "ALL THE WAY ROUND THE MULBERRY BUSH THE MONKEY CHASED THE WEASEL. THE MONKEY THOUGHT 'TWAS ALL IN FUN": GOSUB 600
 12103  XDRAW 1 AT 168,56:TP$ = "POP! GOES THE WEASEL": GOSUB 600: RETURN 
 12200  SCALE= 2: XDRAW 2 AT 123,48: SCALE= 1: DRAW 1 AT 123,32:TP$ = "HUMPTY DUMPTY SAT ON A WALL. HUMPTY DUMPTY HAD A GREAT FALL.": GOSUB 600
 12203  SCALE= 2: DRAW 2 AT 123,48: SCALE= 1: XDRAW 1 AT 123,32: ROT= 90: DRAW 1 AT 123,72:TP$ = "ALL THE KING'S HORSES AND ALL THE KING'S MEN COULDN'T PUT HUMPTY TOGETHER AGAIN.": GOSUB 600
 12205  XDRAW 1 AT 123,72: ROT= 0: RETURN 
 12300 TP$ = "LITTLE BO PEEP HAD LOST HER SHEEP AND DIDN'T KNOW WHERE TO FIND THEM.": GOSUB 600: FOR I = 70 TO 189 STEP 119: FOR J = 40 TO 72 STEP 16: DRAW 1 AT I,J: NEXT J,I
 12303 TP$ = "LEAVE THEM ALONE AND THEY'LL COME HOME WAGGING THEIR TAILS BEHIND THEM.": GOSUB 600: FOR I = 70 TO 189 STEP 119: FOR J = 40 TO 72 STEP 16: XDRAW 1 AT I,J: NEXT J,I:CT% = 0: RETURN 
 32000 T1% =  PEEK (222): CALL 1002: IF T1% = 5 OR T1% = 6 OR T1% = 7 OR T1% = 13 THEN  RESUME 
 32003  NORMAL : TEXT : PRINT : PRINT CH$
 32005  IF T1% = 255 THEN  CALL 520"PR.SAVE"
 32008  CALL 520"PR.ERR"
