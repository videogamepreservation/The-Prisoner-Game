 1  TEXT : HOME : CLEAR : NORMAL : SPEED= 255:ZZ =  FRE (0): POKE  - 16368,0
 2  HIMEM: 22000: LOMEM: 16385
 3  ONERR  GOTO 32000
 8  PRINT  CHR$ (4);"NOMON I,O,C": HOME 
 9  GOTO 1000
 305  VTAB 23: HTAB 1: INVERSE : PRINT "** PRESS <SPACE BAR> TO CONTINUE...   ";: POKE  - 16368,0
 306 A% =  PEEK ( - 16384): IF A% <  > 160 THEN 306
 308  POKE  - 16368,0: NORMAL : RETURN 
 320  HOME : VTAB 18: HTAB 3: PRINT "RESIGNATION CODE ";: FLASH : PRINT TP$:RC$ = TP$: NORMAL 
 390  VTAB 1: INVERSE : FOR I3 = 1 TO 23: HTAB L1%: PRINT " ": NEXT I3: NORMAL : RETURN 
 397  INVERSE :T1% = L6% - L5% + 1: VTAB H5%: HTAB L5%: PRINT  SPC( T1%): VTAB H6%: HTAB L5%: PRINT  SPC( T1%)
 398  VTAB H5% + 1: FOR I3 = H5% + 1 TO H6% - 1: HTAB L5%: PRINT " ";: HTAB L6%: PRINT " ": NEXT I3: NORMAL : RETURN 
 400  VTAB 1: PRINT : PRINT  CHR$ (4);"BLOAD TONES"
 405  FOR I = 9 TO 11: HTAB 13: VTAB I: PRINT  SPC( 14): NEXT I: VTAB 10: HTAB 14
 410  FOR I = 1 TO  LEN (TP$):A$ =  MID$ (TP$,I,1):A% =  ASC (A$) - 21: POKE 38144,A%: POKE 38145,A%: CALL 38146: PRINT A$;: NEXT I: PRINT : RETURN 
 530  FOR I5 = 1 TO 2500: NEXT I5: RETURN 
 600  FOR I6 = 1 TO  LEN (TP$):A$ =  MID$ (TP$,I6,1): PRINT A$;:TP =  ASC (A$) - 31: POKE P2,TP: POKE P1,TP: CALL P3: NEXT I6: RETURN 
 610 TP$ = "": FOR I6 = 1 TO F1%:TP$ = TP$ +  CHR$ ( INT ( RND (1) * 10) + 48): NEXT I6: RETURN 
 1000  FOR I = 1 TO 39 STEP 3:L1% = I: GOSUB 390: NEXT I:TP$ = "THE PRISONER": GOSUB 400: VTAB 24
 1005  GOSUB 2000
 1010  GOSUB 2100
 1030  IF GM% = 1 THEN  GOSUB 2300
 1040  GOSUB 2400
 1050  IF GM% = 1 THEN  GOSUB 5000
 1080  REM INVERSE: FOR I = 1 TO 1000: VTAB  RND (1) * 23 + 1: HTAB  RND (1) * 40 + 1: PRINT " ";: NEXT I: VTAB 1: HTAB 1: FOR I = 1 TO 23: PRINT  SPC( 40): NEXT I: NORMAL  
 1085  VTAB 1: PRINT : VTAB 24
 1086  PRINT D$;"BLOAD PR.SHAPE"
 1090  PRINT CH$
 1095  CALL 520"PR.SQ1"
 1099  END 
 2000  REM  ARRAY SET-UP
 2001  DIM T1%(20,20)
 2003 D$ =  CHR$ (4):CH$ = D$ + "BLOAD CHAIN,A520"
 2005 P1 = 38144:P2 = 38145:P3 = 38146:BK$ = "": FOR I = 1 TO 38:BK$ = BK$ + " ": NEXT I
 2008  DIM PS%(15): DIM PS$(15):PS$(1) = "GOLD WATCH":PS$(2) = "BLACK TIE":PS$(3) = "DIPLOMA":PS$(4) = "CROSS"
 2010 PS$(5) = "PERCOLATOR":PS$(6) = "SILVER DOLLAR"
 2012 PS$(7) = "MAP":PS$(8) = "COMPASS":PS$(9) = "WATCH":PS$(10) = "MATCHES":PS$(11) = "PEN":PS$(12) = "PAPER":PS$(13) = "GOLD PAINT":PS$(14) = "BOOK"
 2014  DIM VC$(69),VC%(69),VN%(12)
 2099  RETURN 
 2100  PRINT D$;"OPEN PR.STATUS"
 2103  PRINT D$;"READ PR.STATUS"
 2105  INPUT GM%: INPUT CR%: INPUT ID%: INPUT RC$: INPUT BN$: INPUT FD%: INPUT MR%: INPUT BR%
 2106  FOR I = 0 TO 15: INPUT PS%(I): NEXT I
 2107  PRINT D$;"CLOSE"
 2110  PRINT D$;"OPEN PR.VOCAB"
 2113  PRINT D$;"READ PR.VOCAB"
 2115  FOR I = 1 TO 69: INPUT VC$(I): INPUT VC%(I): NEXT I
 2118  FOR I = 1 TO 12: INPUT VN%(I): NEXT I
 2120  PRINT D$;"CLOSE"
 2199  RETURN 
 2300  TEXT : HOME 
 2303 L5% = 13:L6% = 26:H5% = 6:H6% = 10: GOSUB 397
 2305 TP$ = "TOP SECRET": VTAB 8: HTAB 15: GOSUB 600: FLASH : HTAB 15: PRINT TP$: GOSUB 530
 2308  HOME : INVERSE : PRINT "RESIGNATION FILE":L5% = 1:L6% = 39:H5% = 2:H6% = 23: GOSUB 397
 2310  NORMAL : VTAB 4: HTAB 3: PRINT "CODE FOLLOWS";: HTAB 23: PRINT "MACHINE SCORE": HTAB 3: PRINT "AS";: HTAB 24: PRINT "REVIEW BELOW"
 2312  VTAB 7:F1% = 13: FOR I = 1 TO 4: GOSUB 610: HTAB 23: PRINT TP$: NEXT I
 2314  VTAB 12: HTAB 3: PRINT "AUTHORITY: US IN-T": HTAB 3: PRINT "CLASSIFICATION: TOP SECRET": HTAB 3: PRINT "PRIORITY: SOCIETAL"
 2316  HTAB 3: PRINT "SUBJECT: RESIGNATION": HTAB 3: PRINT "CROSSFILE: SUB.MISSION": HTAB 4: PRINT "PROJECT ANTIBODY": PRINT : HTAB 3: PRINT "THIS FILE IS CODED AS ISLE"
 2318 F1% = 12: VTAB 6: HTAB 3: GOSUB 610: GOSUB 600:F1% = 3: VTAB 21: HTAB 3: PRINT "RESIGNATION CODE: ";: GOSUB 610: GOSUB 600: GOSUB 530
 2320 BN$ = "": FOR I = 1 TO 5:BN$ = BN$ +  STR$ ( INT ( RND (1) * 9 + 1)): NEXT I
 2399 RC$ = TP$: RETURN 
 2400  HOME : VTAB 21: HTAB 3: PRINT "RESIGNATION CODE:";: FLASH : PRINT RC$: NORMAL 
 2403 H5% = 2:H6% = 10:L5% = 1:L6% = 39: GOSUB 397: INVERSE : VTAB 2: HTAB 13: PRINT "CONFIDENTIAL": NORMAL : VTAB 4: HTAB 2
 2405  PRINT "BELOW IS YOUR RESIGNATION CODE. IT": HTAB 2: PRINT "SIGNIFIES YOUR REASONS FOR LEAVING": HTAB 2: PRINT "THE COMPANY. COMMIT IT TO MEMORY,": HTAB 2: PRINT "WRITE IT DOWN, BUT UNDER NO CIRCUM-"
 2408  HTAB 2: PRINT "STANCES REVEAL IT TO ANYONE.": GOSUB 305: PRINT : NORMAL : HOME : RETURN 
 5000  VTAB 1: PRINT "WHERE DO YOU WISH TO GO?": VTAB 3: FOR I = 1 TO 40: PRINT "-";: NEXT I: VTAB 5: HTAB 1
 5001  PRINT "LONDON": PRINT : PRINT "NEW YORK": PRINT : PRINT "GENEVA": PRINT 
 5003  PRINT "PARIS": PRINT : PRINT "TOKYO": PRINT : PRINT "MADRID"
 5005 : PRINT : FOR I = 1 TO 40: PRINT "-";: NEXT I: VTAB 22: HTAB 1: PRINT "ENTER DESTINATION:";: FOR I = 1 TO 3: GET A$: PRINT A$;: NEXT I: PRINT : RETURN 
 32000  CALL 1002: RESUME 
