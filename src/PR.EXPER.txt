 1  TEXT : HOME :ZZ =  FRE (0): POKE  - 16368,0
 2  ONERR  GOTO 32000
 5  PRINT D$;"BLOAD TONES"
 9  GOTO 1000
 300  INVERSE : HTAB 5: SPEED= 200: PRINT TP$: SPEED= 255: NORMAL : RETURN 
 310 A% =  PEEK ( - 16384): IF A% < 127 THEN 310
 311  POKE  - 16368,0:A% = A% - 128: RETURN 
 351  FOR I3 = 1 TO 1000: NEXT I3: RETURN 
 395 T1% = L4% - L3% + 1: VTAB H3%: FOR I3 = H3% TO H4%: HTAB L3%: PRINT  SPC( T1%): PRINT : NEXT I3: RETURN 
 397  INVERSE :T1% = L6% - L5% + 1: VTAB H5%: HTAB L5%: PRINT  SPC( T1%): VTAB H6%: HTAB L5%: PRINT  SPC( T1%)
 398  VTAB H5% + 1: FOR I3 = H5% + 1 TO H6% - 1: HTAB L5%: PRINT " ";: HTAB L6%: PRINT " ": NEXT I3: NORMAL : RETURN 
 400  COLOR= 15 -  INT (T4% / 4): PLOT X2%,Y2%: FOR I4 = 1 TO 5: NEXT I4: COLOR= 0: PLOT X2%,Y2%: RETURN 
 410  POKE P1,20: POKE P2,17: CALL P3: RETURN 
 430  POKE P2,(T4% + 1) * 10: POKE P1,250: CALL P3: RETURN 
 440 IP$ = "": FOR I4 = 1 TO 3:IP$ = IP$ +  STR$ ( INT ( RND (1) * 10)): NEXT I4: IF IP$ = RC$ THEN 440
 441 IP$ = IP$ + "  ": RETURN 
 450 X2% = X2% + 1: IF X2% > 35 THEN X2% = 8
 451  GOSUB 400: IF X2% / 15 =  INT (X2% / 15) THEN T1% = Y2%:T2% = 4 + T4% / 2: FOR J = T1% TO T2% STEP  - 1:Y2% = J: GOSUB 400: NEXT J: GOSUB 410: FOR J = T2% TO T1%:Y2% = J: GOSUB 400: NEXT J:Y2% = T1%: RETURN 
 452  RETURN 
 460  FOR I5 = 1 TO 30: GOSUB 450: NEXT I5: RETURN 
 1000  GOSUB 2000
 1005 ID% = ID% - T4% * 2
 1010  NORMAL : PRINT : PRINT CH$
 1015  CALL 520"PR.VILLAGE"
 2000  INVERSE : FOR I = 1 TO 23: PRINT  SPC( 40): NEXT I:T1$ = "          +++++*****": VTAB 22: HTAB 17: PRINT "RESPONSE": VTAB 24: HTAB 2: PRINT " VOLTAGE:";: HTAB 31: PRINT  SPC( 9)
 2003  NORMAL :H3% = 3:H4% = 11:L3% = 5:L4% = 36: GOSUB 395: VTAB 21: HTAB 18: PRINT  SPC( 6): PRINT : HTAB 3: PRINT " ";: HTAB 5: PRINT  SPC( 8): VTAB 24: HTAB 11: PRINT T1$;
 2005 H5% = 4:H6% = 10:L5% = 12:L6% = 25: GOSUB 397: NORMAL : VTAB 6: FOR I = 1 TO 3: HTAB 25: PRINT " ": NEXT I: VTAB 7: HTAB 26: PRINT "#"
 2008  VTAB 13:TP$ = "GOOD-HE IS ENTERING THE BUILDING": GOSUB 300: GOSUB 351: VTAB 7: FOR I = 25 TO 22 STEP  - 1: HTAB I: PRINT "# ";: GOSUB 351: NEXT I: PRINT  CHR$ (7);: HTAB 22: PRINT " "
 2010  VTAB 14:TP$ = "WE ARE NOW ATTACHING ELECTRODES": GOSUB 300:TP$ = "TO OUR PRISONER. TO APPLY": GOSUB 300
 2013 TP$ = "VOLTAGE PRESS THE V KEY - TRY IT": GOSUB 300
 2015  GOSUB 310: IF A% <  >  ASC ("V") THEN  PRINT  CHR$ (7);: GOTO 2015
 2018 TP$ = "TO INCREASE THE VOLTAGE PRESS": GOSUB 300:TP$ = "THE I KEY - TRY IT": GOSUB 300
 2020  GOSUB 310: IF A% <  >  ASC ("I") THEN  PRINT  CHR$ (7);: GOTO 2020
 2023 H3% = 13:H4% = 20: INVERSE : GOSUB 395: VTAB 13:TP$ = "IF YOU ARE SUCCESSFUL IN GETTING": GOSUB 300:TP$ = "THE PRISONER TO REVEAL HIS REA-": GOSUB 300
 2025 TP$ = "SON FOR RESIGNING, YOU WILL BE": GOSUB 300:TP$ = "PROMOTED FROM CARETAKER AND": GOSUB 300:TP$ = "REASSIGNED OFF OF THE ISLAND.": GOSUB 300
 2028 TP$ = "IF HE ANSWERS OUR QUESTIONS": GOSUB 300:TP$ = "PRESS E TO ENTER HIS RESPONSE": GOSUB 300: GOSUB 310: INVERSE 
 2030  GOSUB 395: VTAB 13:TP$ = "IF YOU WISH TO ABORT THE EXPERI-": GOSUB 300:TP$ = "MENT, PRESS THE ESC KEY. HOWEVER": GOSUB 300:TP$ = "THE MASTER WILL NOT BE PLEASED": GOSUB 300: GOSUB 310
 2033  GR : COLOR= 15: FOR I = 0 TO 39: HLIN 0,39 AT I: NEXT I: COLOR= 0: FOR I = 4 TO 21: HLIN 5,36 AT I: NEXT I
 2035 CT% = 0:X2% = 8:Y2% = 14:T4% = 0: FOR I = 0 TO 20
 2038 A% =  PEEK ( - 16384): IF A% > 127 THEN 2100
 2040  GOSUB 450: GOTO 2038
 2100  POKE  - 16368,0: IF A% = 155 THEN I = 30: NEXT I: RETURN 
 2103 A% = A% - 128: IF A% =  ASC ("I") AND CT% = T4% AND CT% < 20 THEN CT% = CT% + 1: FLASH : VTAB 24: HTAB 10 + CT%: PRINT  MID$ (T1$,CT%,1);: NORMAL : NEXT I: RETURN 
 2105  IF A% =  ASC ("V") THEN 2200
 2108  IF A% =  ASC ("E") THEN 2300
 2110  GOTO 2038
 2200 IP$ = "":T4% = CT%: GOSUB 430: IF T4% >  INT ( RND (1) * 20) THEN IP$ = "UHHHH": VTAB 21: HTAB 18: PRINT IP$: GOSUB 460: GOTO 2202
 2201  GOTO 2038
 2202 IP$ = "": IF T4% >  INT ( RND (1) * 20) THEN  GOSUB 440: IF T4% = 20 THEN IP$ = RC$ + "  "
 2203  IF  NOT ( LEN (IP$)) THEN IP$ = "     ": IF T4% > 5 THEN IP$ = "NEVER": IF T4% > 10 THEN IP$ = "STOP!": IF T4% > 15 THEN IP$ = "HURTS"
 2204  VTAB 21: HTAB 18: PRINT IP$: GOSUB 460: VTAB 21: HTAB 18: PRINT  SPC( 5): GOTO 2038
 2300 ID% = ID% + 1: FLASH : VTAB 22: HTAB 3: PRINT " ";: NORMAL : GOSUB 460: HTAB 5: IF IP$ <  > RC$ THEN  PRINT "NEGATIVE";: HTAB 3: PRINT " ": GOTO 2038
 2310  PRINT "POSITIVE":I = 30: NEXT I: POP : GOTO 13000
 2999  END 
 13000  NORMAL : PRINT : PRINT CH$
 13003  CALL 520"PR.LOSE"
 32000 T1% =  PEEK (222): CALL 1002: IF T1% = 5 OR T1% = 6 OR T1% = 7 OR T1% = 13 THEN  RESUME 
 32003  NORMAL : PRINT : PRINT CH$
 32005  IF T1% = 255 THEN  CALL 520"PR.SAVE"
 32008  CALL 520"PR.ERR"
