 1  TEXT : HOME :ZZ =  FRE (0): POKE  - 16368,0
 2  ONERR  GOTO 32000
 5  PRINT D$;"BLOAD TONES"
 9  GOTO 1000
 395 : VTAB H3%:T1% = L4% - L3% + 1: FOR I3 = H3% TO H4%: HTAB L3%: PRINT  SPC( T1%): PRINT : NEXT I3: RETURN 
 397  INVERSE :T1% = L6% - L5% + 1: VTAB H5%: HTAB L5%: PRINT  SPC( T1%): VTAB H6%: HTAB L5%: PRINT  SPC( T1%)
 398  VTAB H5% + 1: FOR I3 = H5% + 1 TO H6% - 1: HTAB L5%: PRINT " ";: HTAB L6%: PRINT " ": NEXT I3: NORMAL : RETURN 
 531  FOR I5 = 1 TO 1000: NEXT I5: RETURN 
 600  VTAB Y%: HTAB X%: PRINT "#";
 603 A% =  PEEK ( - 16384): IF A% < 127 THEN ZT = ZT + 1: GOTO 603
 605  POKE  - 16368,0: VTAB Y%: HTAB X%: PRINT " ";:A% = A% - 128:X1% = 0:Y1% = 0: IF A% =  ASC ("U") THEN Y1% =  - 1: RETURN 
 606  IF A% =  ASC ("D") THEN Y1% = 1: RETURN 
 607  IF A% =  ASC ("L") THEN X1% =  - 1: RETURN 
 608  IF A% =  ASC ("R") THEN X1% = 1: RETURN 
 609 T1% = 100:T2% = 17: GOSUB 610: GOTO 600
 610  POKE P1,T1%: POKE P2,T2%: CALL P3: RETURN 
 620  POKE P1,255: POKE P2,255: CALL P3: RETURN 
 700 T1%(X% - 11,Y% - 8) = 1: RETURN 
 800 L3% = 3:L4% = 38:H3% = 2:H4% = 4: GOSUB 395: ON CT% GOSUB 13000,13001,13002,13004
 801  HTAB 3: VTAB 2: GOSUB 610: PRINT "YOU FOUND A ";A$: HTAB 3: PRINT "WHAT WILL YOU DO WITH IT?"
 802 T2$ = "I SEE"
 803  HTAB 3: GOSUB 850: GOSUB 397: FOR I = 1 TO  LEN (IP$) - 3:T1$ =  MID$ (IP$,I,4): IF (T1$ = "RAFT" OR T1$ = "SAIL" OR T1$ = "SWIM" OR T1$ = "CROS") AND CT% < 4 THEN T2$ = "SOUNDS DOUBTFUL":I = 40
 804  NEXT I: GOSUB 395: VTAB 3: HTAB 3: PRINT T2$: RETURN 
 850 IP$ = "":T8% = 0: POKE  - 16368,0
 851  GET A$: IF A$ =  CHR$ (8) THEN 851
 852 T8% = T8% + 1:IP$ = IP$ + A$: PRINT A$;: IF T8% < 30 AND A$ <  >  CHR$ (13) THEN 851
 853  RETURN 
 1000  GOSUB 10000
 1005  GOSUB 2000
 1010  GOSUB 3000
 1020  GOSUB 4000
 1030  GOSUB 2000
 1035  PRINT CH$
 1040  CALL 520"PR.WILD"
 1999  END 
 2000  GOSUB 12000: VTAB 2: HTAB 3: PRINT "CROSS THE PIT BY USE OF THE";: HTAB 36: PRINT "U"
 2003  HTAB 3: PRINT "NETWORK OF RAMPS BELOW. DO NOT  L+R": HTAB 3: PRINT "TRY JUMPING OR GOING AROUND";: HTAB 36: PRINT "D"
 2005 X% = 14 +  INT ( RND (1) * 10):Y% = 17: FOR I = 1 TO 20: FOR J = 1 TO 20:T1%(I,J) = 0: NEXT J,I:Y1% = 17
 2008 T1% =  INT ( RND (1) * (Y% - 8 + 1)): IF (Y% - T1% > Y1% - 2) AND (Y% - T1% <  > 8) THEN 2008
 2009  FOR I = 1 TO T1%: VTAB Y%: HTAB X%: PRINT "D": GOSUB 700:Y% = Y% - 1: NEXT I:X1% = X%: IF Y% = 8 THEN 2020
 2010 T1% =  INT ( RND (1) * (X% - 13)) + 1: FOR I = 1 TO T1%: VTAB Y%: HTAB X%: PRINT "R": GOSUB 700:X% = X% - 1: NEXT I
 2012 T1% =  INT ( RND (1) * (Y% - 8)) + 1: FOR I = 1 TO T1%: VTAB Y%: HTAB X%: PRINT "D": GOSUB 700:Y% = Y% - 1: NEXT I: IF Y% = 8 THEN 2020
 2014 T1% =  INT ( RND (1) * (24 - X1%) + (X1% - X%)) + 2: FOR I = 1 TO T1%: VTAB Y%: HTAB X%: PRINT "L": GOSUB 700:X% = X% + 1: NEXT I:Y1% = Y%
 2016 T1% =  INT ( RND (1) * (14 - Y%)) + 1: FOR I = 1 TO T1%: VTAB Y%: HTAB X%: PRINT "U": GOSUB 700:Y% = Y% + 1: NEXT I:
 2018 T1% =  INT ( RND (1) * (26 - X%)) + 2: FOR I = 1 TO T1%: VTAB Y%: HTAB X%: PRINT "L": GOSUB 700:X% = X% + 1: NEXT I
 2019  GOTO 2008
 2020  VTAB Y%: HTAB X%: PRINT "D": GOSUB 700
 2025 X% = 20:Y% = 23
 2030  NORMAL : GOSUB 600
 2033  IF Y% + Y1% < 9 THEN  FLASH : VTAB 8: HTAB (X% > 20) * 30 + 1: PRINT  SPC( 10): GOSUB 620: NORMAL : GOTO 2030
 2035  IF X% + X1% < 1 OR X% + X1% > 40 OR Y% + Y1% < 6 OR Y% + Y1% > 23 THEN T1% = 35:T2% = 17: GOSUB 620: GOTO 2030
 2038 X% = X% + X1%:Y% = Y% + Y1%: IF (X% < 31 AND X% > 10) AND (Y% < 18 AND Y% > 7) THEN 2100
 2040  GOTO 2030
 2100  INVERSE : IF Y% = 17 THEN  NORMAL 
 2103  VTAB Y%: HTAB X%: PRINT "#"
 2105  VTAB Y% - Y1%: HTAB X% - X1%: PRINT " ": INVERSE : IF Y% < 8 THEN  NORMAL : VTAB Y%: HTAB X%: PRINT "#": RETURN 
 2106  IF  NOT (T1%(X% - 11,Y% - 8)) THEN  POP : GOTO 9000
 2108 T1%(X% - 11,Y% - 8) = 0: GOSUB 600:X% = X% + X1%:Y% = Y% + Y1%: GOTO 2100
 3000  VTAB Y%: HTAB X%: PRINT "#": GOSUB 531: HOME : GOSUB 12000:Y% = 23: HTAB X%: VTAB Y%: PRINT "#":ID% = ID% + 20
 3003  VTAB 2: HTAB 3: PRINT "CROSS THE PIT BY ANY MEANS AT YOUR": HTAB 3: PRINT "DISPOSAL. HOWEVER, DO NOT GO AROUND": HTAB 3: PRINT "NOR ALLOW YOURSELF TO FALL IN IT."
 3004 CT% = 0
 3010  NORMAL : GOSUB 600
 3013  IF Y% + Y1% < 9 THEN  FLASH : VTAB 8: HTAB (X% > 20) * 30 + 1: PRINT  SPC( 10): GOSUB 620: NORMAL : GOTO 3010
 3015  IF X% + X1% < 1 OR X% + X1% > 40 OR Y% + Y1% < 6 OR Y% + Y1% > 23 THEN T1% = 35:T2% = 17: GOSUB 620: GOTO 3010
 3018 X% = X% + X1%:Y% = Y% + Y1%: IF (X% > 10 AND X% < 31) AND (Y% > 7 AND Y% < 18) THEN 3100
 3020  IF  ABS (20.5 - X% + X1%) > 13 AND  NOT ( INT ( RND (1) * 20 * (CT% + 1))) THEN CT% = CT% + 1: IF CT% < 5 THEN  GOSUB 800
 3025  GOTO 3010
 3100  INVERSE : GOSUB 600: IF X% + X1% < 11 OR X% + X1% > 30 OR Y% + Y1% > 17 THEN T1% = 100:T2% = 17: GOSUB 610: GOTO 3100
 3102  IF Y% = 17 THEN  NORMAL : VTAB 16: HTAB X% - X1%: PRINT " "
 3103  IF Y% = 7 THEN ID% = ID% + 50: RETURN 
 3105  IF Y% < 12 AND CT% < 4 THEN H3% = 2:H4% = 4:L3% = 3:L4% = 38: NORMAL : GOSUB 395: VTAB 3: HTAB 3: PRINT "SORRY...YOU DIDN'T MAKE IT": GOSUB 531: POP : GOTO 9000
 3108 Y% = Y% + Y1%:X% = X% + X1%: GOTO 3100
 4000  GOSUB 12000: VTAB 2: HTAB 3: PRINT "CROSS THE PIT ONLY WHEN YOU SEE THE": HTAB 3: PRINT "TWO DOTS IN THE CENTER MOVE TOWARDS": HTAB 3: PRINT "EACH OTHER"
 4003  INVERSE : VTAB 12: HTAB 15: PRINT ".";: HTAB 25: PRINT ".":ZT = 0:Y% = 23
 4010  NORMAL : GOSUB 600
 4013  IF Y% + Y1% < 9 THEN  RETURN 
 4015  IF X% + X1% < 1 OR X% + X1% > 40 OR Y% + Y1% < 6 OR Y% + Y1% > 23 THEN T1% = 35:T2% = 17: GOSUB 620: GOTO 4010
 4018 X% = X% + X1%:Y% = Y% + Y1%: IF  ABS (20.5 - X% + X1%) <  = 9.5 AND  ABS (12.5 - Y% + Y1%) <  = 5.5 THEN ID% = ID% - 20 +  LOG (ZT + 1): POP : GOTO 9000
 4020  GOTO 4010
 9000  POKE P2,2: FOR I = 10 TO 150 STEP 10: POKE P1,I: CALL P3: NEXT I
 9001  PRINT : PRINT CH$
 9002  CALL 520"PR.SQ1"
 10000 H5% = 3:H6% = 9:L5% = 2:L6% = 39: GOSUB 397:H5% = 12:H6% = 17:L5% = 2:L6% = 19: GOSUB 397:L5% = 22:L6% = 39: GOSUB 397
 10003  VTAB 5: HTAB 18: PRINT "ISLAND": HTAB 16: PRINT "RECREATION": HTAB 19: PRINT "HALL"
 10005  VTAB 13: HTAB 10: PRINT "--";: HTAB 25: PRINT "A HEALTHY BODY": HTAB 4: PRINT "ESCAPE THROUGH";: HTAB 28: PRINT "MAKES"
 10008  HTAB 3: PRINT "PHYSICAL FITNESS";: HTAB 24: PRINT "A TRANQUIL MIND": HTAB 10: PRINT "--";: HTAB 28: PRINT "+ + +"
 10010  VTAB 20: PRINT : PRINT "WILL YOU PAY THE 5 CREDIT FEE?";: GET A$: PRINT A$: IF A$ = "Y" THEN 10015
 10012  POP : PRINT CH$
 10013  CALL 520"PR.VILLAGE"
 10015  POKE  - 16368,0: VTAB 6: HTAB 18: PRINT "WRECK": HOME : RETURN 
 12000  HOME :H5% = 1:H6% = 5:L5% = 2:L6% = 39: GOSUB 397: INVERSE :H3% = 8:H4% = 17:L3% = 11:L4% = 30: GOSUB 395: NORMAL : RETURN 
 13000 A$ = "PIECE OF ROPE": RETURN 
 13001 A$ = "BUNDLE OF STICKS": RETURN 
 13002 A$ = "RUSTY, OLD WASH TUB": RETURN 
 13004 A$ = "INFLATABLE RAFT": RETURN 
 32000 T1% =  PEEK (222): CALL 1002: IF T1% = 5 OR T1% = 6 OR T1% = 7 OR T1% = 13 THEN  RESUME 
 32003  NORMAL : PRINT : PRINT CH$
 32005  IF T1% = 255 THEN  CALL 520"PR.SAVE"
 32008  CALL 520"PR.ERR"
