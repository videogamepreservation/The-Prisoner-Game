 1 ZZ =  FRE (0): POKE  - 16368,0
 2  ONERR  GOTO 32000
 5  PRINT D$;"BLOAD TONES"
 9  GOTO 1000
 395 T1% = L4% - L3% + 1: VTAB H3%: FOR I3 = H3% TO H4%: HTAB L3%: PRINT  SPC( T1%): PRINT : NEXT I3: RETURN 
 600  VTAB 2: HTAB 1: SPEED= 50: PRINT TP$: VTAB 2: HTAB 1: PRINT  SPC( 40): SPEED= 255: RETURN 
 610  VTAB 3: HTAB 1: PRINT  SPC( 20): HTAB 1:IP$ = "":CT% = 0
 613  GET A$: PRINT A$;: IF CT% > 19 OR A$ =  CHR$ (13) THEN 618
 615 CT% = CT% + 1:IP$ = IP$ + A$: GOTO 613
 618 IP$ = IP$ + "    ": RETURN 
 1000  NORMAL : FOR I = 0 TO 19: FOR J = 1 TO 24: VTAB J: HTAB 20 - I: PRINT  SPC( 2 + I * 2): NEXT J,I: HOME 
 1002  INVERSE :L3% = 2:L4% = 20:H3% = 2:H4% = 16: GOSUB 395:L3% = 22:L4% = 39:H3% = 2:H4% = 10: GOSUB 395
 1003  VTAB 2: HTAB 7: PRINT "DIRECTORY": PRINT : HTAB 3: PRINT "<1> THE COLONEL": PRINT : HTAB 3: PRINT "<2> THE GENERAL": PRINT : HTAB 3: PRINT "<3> THE DIRECTOR": PRINT 
 1005  HTAB 3: PRINT "<4> THE CHIEF": PRINT : HTAB 3: PRINT "<5> THE SECRETARY": PRINT : HTAB 3: PRINT "WHO DO YOU WISH?";
 1006  GET A$:A% =  VAL (A$): IF A% < 1 OR A% > 5 THEN 1006
 1007  PRINT A$
 1008  VTAB 2: HTAB 26: PRINT "FINGERPRINT": HTAB 27: PRINT "ANALYZER": PRINT : HTAB 23: PRINT "PLACE YOUR": HTAB 23: PRINT "FINGERS ON THE": HTAB 23: PRINT "FOLLOWING KEYS"
 1010  PRINT : HTAB 23: PRINT "A S D F J K L ;":T2(1) = 65:T2(2) = 83:T2(3) = 68:T2(4) = 70:T2(5) = 74:T2(6) = 75:T2(7) = 76:T2(8) = 59: NORMAL 
 1013  FOR I = 12 TO 14: VTAB I: HTAB 22: FOR J = 22 TO 39: PRINT  INT ( RND (1) * 10);: NEXT J,I:CT% = 0
 1015 A% =  PEEK ( - 16384): IF A% < 127 THEN 1015
 1018 A% = A% - 128: POKE  - 16368,0: FOR I = 1 TO 8: IF A% = T2(I) THEN  VTAB 9: HTAB I * 2 + 21: PRINT  CHR$ (A%):T2(I) = 0:I = 9:CT% = CT% + 1
 1020  NEXT I: IF CT% < 8 THEN 1015
 1023  FOR I = 1 TO 50:T1% =  INT ( RND (1) * 3) + 12:T2% =  INT ( RND (1) * 18) + 22: INVERSE : VTAB T1%: HTAB T2%:T3% =  INT ( RND (1) * 10): PRINT T3%;: POKE P1,T1%: POKE P2,T2%: CALL P3: HTAB T2%: NORMAL : PRINT T3%: NEXT I
 1025  FLASH : VTAB 16: HTAB 22: PRINT "++RESIGNED AGENT++": POKE P1,250: POKE P2,250: FOR I = 1 TO 10: CALL P3: NEXT I: NORMAL 
 1028  FOR I = 1 TO 2500: NEXT I: INVERSE : VTAB 19: HTAB 13: PRINT "CLEARANCE GIVEN": NORMAL : FOR I = 5 TO 1 STEP  - 1: FOR J = 1 TO 24 STEP I: VTAB J: PRINT  SPC( 40): NEXT J,I
 1030  GOSUB 10000
 1035 TP$ = "WELL, WELL. LOOK WHO'S RETURNED": GOSUB 600:TP$ = "WE HAD GIVEN YOU UP FOR LOST": GOSUB 600
 1038 TP$ = "HAVE A SEAT": GOSUB 600:TP$ = "YOU LOOK NONE THE WORSE FOR THE ABSENCE": GOSUB 600
 1040 TP$ = "IT IS A STRANGE SITUATION WE HAVE HERE": GOSUB 600:TP$ = "FIRST YOU RESIGN": GOSUB 600:TP$ = "THEN YOU DISAPPEAR WITHOUT A TRACE": GOSUB 600:TP$ = "AND NOW YOU RETURN": GOSUB 600
 1043 TP$ = "SOME OF US THOUGHT YOU HAD GONE OVER": GOSUB 600:TP$ = "TO THE OTHER SIDE. HAVE YOU?": GOSUB 600: GET IP$: PRINT 
 1045  GET IP$: IF IP$ <  > "Y" AND IP$ <  > "N" THEN TP$ = "SIMPLY ANSWER YES OR NO": GOSUB 600: GOTO 1045
 1058  IF IP$ = "Y" THEN TP$ = "AS YOU KNOW": GOSUB 600:TP$ = "DEFECTORS ARE NOT TREATED KINDLY HERE": GOSUB 600: GOTO 2000
 1060 TP$ = "SORRY TO SEEM DISTRUSTING": GOSUB 600:TP$ = "BUT YOU'LL HAVE TO PROVE IT": GOSUB 600:TP$ = "AND THERE IS ONLY ONE WAY": GOSUB 600
 1100 TP$ = "TELL US WHY YOU RESIGNED. THE CODE": GOSUB 600: GOSUB 610: FOR I = 1 TO  LEN (IP$) - 3: IF  MID$ (IP$,I,3) = RC$ THEN I = 500: NEXT I: GOTO 30000
 1110 TP$ = "NOT READY TO TELL US?": GOSUB 600:TP$ = "YOU WERE LOYAL. THAT'S NOT LIKE YOU": GOSUB 600:TP$ = "WE'LL LET YOU THINK A WHILE": GOSUB 600: GOTO 2000
 2000  TEXT : HOME : INVERSE : FOR I = 1 TO 40 STEP 3: FOR J = 1 TO 23: VTAB J: HTAB I: PRINT " ": NEXT J,I: POKE  - 16368,0
 2005 A% =  PEEK ( - 16384): IF A% < 127 THEN 2005
 2010  IF A% = 155 THEN 3000
 2015  GOSUB 10000:TP$ = "YOU'VE HAD TIME TO THINK": GOSUB 600: GOTO 1100
 3000  PRINT : PRINT CH$
 3003  CALL 520"PR.SQ1"
 10000  HOME :L3% = 6:L4% = 35:H3% = 14:H4% = 23: INVERSE : GOSUB 395: VTAB 14: HTAB 3: PRINT  SPC( 36): VTAB 24: HTAB 6: PRINT  SPC( 1): HTAB 35: PRINT  SPC( 1):L3% = 16:L4% = 23:H3% = 7: GOSUB 395
 10005 H3% = 4:H4% = 8:L3% = 28:L4% = 38: GOSUB 395: VTAB 5: HTAB 32: PRINT "THE": PRINT : HTAB 30: PRINT "COMPANY": VTAB 12: NORMAL : HTAB 9: PRINT "/ /": HTAB 8: PRINT "/ /": RETURN 
 30000  TEXT : NORMAL : PRINT : PRINT CH$
 30003  CALL 520"PR.LOSE"
 32000 T1% =  PEEK (222): CALL 1002: IF T1% = 5 OR T1% = 6 OR T1% = 7 OR T1% = 13 THEN  RESUME 
 32003  NORMAL : TEXT : PRINT : PRINT CH$
 32005  IF T1% = 255 THEN  CALL 520"PR.SAVE"
 32008  CALL 520"PR.ERR"
